plugins {
    id 'com.github.rodm.teamcity-server' version "0.9.1"
}

teamcity {
    version = teamcityVersion

    server {
        descriptor = project.file('teamcity-server-plugin.xml')
        tokens = [Plugin_Version: project.version]

        files {
            into('bin') {
                from (project(':nuget-extensions').file('bin')) {
                    include 'plugins-*/JetBrains*'
                }
            }

            into('licenses') {
                from (project.file('../licenses'))
            }
        }
    }
}

dependencies {
    agent project(path: ':nuget-agent', configuration: 'plugin')

    compile project(':nuget-common')
    compile project(':nuget-feed')
    compile project(':nuget-server-common')

    provided 'com.google.code.gson:gson:2.4'
    provided 'org.apache.httpcomponents:httpcore:4.3.3'
    provided 'org.apache.httpcomponents:httpclient:4.3.6'
    provided 'javax.ws.rs:javax.ws.rs-api:2.0'
    provided "org.jetbrains.teamcity.internal:server:$teamcityVersion"
    provided files("${teamcityDir}/webapps/ROOT/WEB-INF/lib/common-impl.jar")
    provided files("${teamcityDir}/webapps/ROOT/WEB-INF/lib/common-tools.jar")
    provided files("${teamcityDir}/webapps/ROOT/WEB-INF/lib/nuget-utils.jar")
    provided files("${teamcityDir}/webapps/ROOT/WEB-INF/lib/server-tools.jar")
}

serverPlugin.version = null
serverPlugin.baseName = 'dotNetPackagesSupport'

build.dependsOn(":nuget-extensions:msbuild")
test.dependsOn(":nuget-tests:test")