ext {
    versionNumber = project.hasProperty('versionNumber') ? property('versionNumber') : 'SNAPSHOT-' + new Date().format('yyyyMMddHHmmss')
    projectIds = ['group': 'teamcity-nuget-support', 'version': versionNumber]

    teamcityVersion = project.hasProperty('teamcityVersion') ? property('teamcityVersion') : '10.0'
    teamcityDir = project.hasProperty('teamcityDir') ? property('teamcityDir') : "$rootDir/teamcity/servers/TeamCity-${teamcityVersion}"
    if (teamcityDir == null || !file(teamcityDir).isDirectory()) {
        throw new InvalidUserDataException('Please, specify correct `teamcityDir` path in gradle.properties file')
    }
    teamcityDir = teamcityDir.replace("\\", "/")
    if (teamcityDir.endsWith("/")) {
        teamcityDir = teamcityDir.substring(0, teamcityDir.length() - 5);
    }
}

group = projectIds.group
version = projectIds.version

allprojects {
    group = projectIds.group
    version = projectIds.version
}

subprojects { subproject ->
    if (!subproject.name.endsWith("extensions")) {
        apply plugin: 'java'

        sourceSets {
            main {
                java {
                    srcDirs = ['src']
                }

                resources {
                    srcDirs = ['resources']
                }
            }

            test {
                java {
                    srcDirs = ['test']
                }

                resources {
                    srcDirs = ['testResources']
                }
            }
        }

        repositories {
            maven { url '../local-repo' }
            maven { url 'http://repository.apache.org/snapshots/' }
        }

        test.useTestNG()
        jar.version = null

        tasks.withType(JavaCompile) {
            sourceCompatibility = "1.8"
            targetCompatibility = "1.8"
        }
    }
}